
#Область ОписаниеПеременных

#КонецОбласти

#Область ОбработчикиСобытийФормы
// Код процедур и функций
#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы
// Код процедур и функций
#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Асинх Процедура Далее(Команда)
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрыДиалога = Новый ПараметрыДиалогаПомещенияФайлов(,, НСтр("ru='Выгрузка данных|*.xml|Все файлы|*'"));
	
	ОписаниеФайла = Ждать ПоместитьФайлНаСерверАсинх(,,, ПараметрыДиалога, УникальныйИдентификатор);
	Если ОписаниеФайла = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ИмпортироватьВыгрузкуНаСервере(ОписаниеФайла.Адрес, ПравилоСравненияДанных);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаСервереБезКонтекста
Функция ИмпортироватьВыгрузкуНаСервере(Знач АдресВыгрузки, Знач ПравилоСравненияДанных)
	ВыгрузкаСтрокой = ПолучитьСтрокуИзДвоичныхДанных(ПолучитьИзВременногоХранилища(АдресВыгрузки));
	
	ЧтениеXML = Новый ЧтениеXML;
	ЧтениеXML.УстановитьСтроку(ВыгрузкаСтрокой);
	
	ВыгрузкаXDTO = ФабрикаXDTO.ПрочитатьXML(ЧтениеXML, ФабрикаXDTO.Тип("http://comparator.iryaboff.ru/v1/", "ВыгрузкаДанных"));
	ЧтениеXML.Закрыть();
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	СравненияДанных.Регистратор КАК Регистратор,
		|	СравнениеДанных.Статус КАК Статус,
		|	СравнениеДанных.ПравилоСравненияДанных КАК ПравилоСравненияДанных
		|ИЗ
		|	РегистрСведений.СравненияДанных КАК СравненияДанных
		|		ВНУТРЕННЕЕ СОЕДИНЕНИЕ Документ.СравнениеДанных КАК СравнениеДанных
		|		ПО СравненияДанных.Регистратор = СравнениеДанных.Ссылка
		|ГДЕ
		|	СравненияДанных.ИдентификаторВыгрузки = &ИдентификаторВыгрузки";
	
	Запрос.УстановитьПараметр("ИдентификаторВыгрузки", ВыгрузкаXDTO.ИдентификаторВыгрузки);
	
	РезультатЗапроса = Запрос.Выполнить().Выгрузить();
	
	НачатьТранзакцию();
	Попытка
		Если РезультатЗапроса.Количество() = 1 Тогда
			Документ = РезультатЗапроса[0].Регистратор.ПолучитьОбъект();
			Документ.Заблокировать();
		Иначе
			Документ = Документы.СравнениеДанных.СоздатьДокумент();
			Документ.Дата = ТекущаяДатаСеанса();
			Документ.ПравилоСравненияДанных = ПравилоСравненияДанных;
			Документ.ИдентификаторВыгрузки = ВыгрузкаXDTO.ИдентификаторВыгрузки;
			Документ.Статус = Перечисления.СтатусыДокументаСравнениеДанных.ОжидаетсяВыгрузкаИзДругойБазы;
			
			Документ.Записать();
			Документ.Заблокировать();
		КонецЕсли;
		
		ЗаписьАрхиваВыгрузок = РегистрыСведений.ВыгрузкиДанных.СоздатьМенеджерЗаписи();
		ЗаписьАрхиваВыгрузок.Документ = Документ.Ссылка;
		ЗаписьАрхиваВыгрузок.База = ?(ВыгрузкаXDTO.НомерБазы = 1, Перечисления.НомераБаз.Первая, Перечисления.НомераБаз.Вторая);
		ЗаписьАрхиваВыгрузок.Выгрузка = Новый ХранилищеЗначения(ВыгрузкаСтрокой, Новый СжатиеДанных(9));
		ЗаписьАрхиваВыгрузок.Записать();
		
		Документ.ОбновитьСтатус();
		Документ.Записать(РежимЗаписиДокумента.Проведение);
		
		ЗафиксироватьТранзакцию();
		Возврат Документ.Ссылка;
	Исключение
		Если ТранзакцияАктивна() Тогда
			ОтменитьТранзакцию();
		КонецЕсли;
		ВызватьИсключение;
	КонецПопытки;
КонецФункции

#КонецОбласти
