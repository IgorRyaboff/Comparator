
#Область ОбработчикиКомандФормы

&НаКлиенте
Асинх Процедура ВыбратьФайл(Команда)
	ПараметрыДиалога = Новый ПараметрыДиалогаПомещенияФайлов(,, НСтр("ru='Выгрузка данных|*.xml|Все файлы|*'"));
	
	ОписаниеФайла = Ждать ПоместитьФайлНаСерверАсинх(,,, ПараметрыДиалога, УникальныйИдентификатор);
	Если ОписаниеФайла = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	АдресВыгрузки = ОписаниеФайла.Адрес;
	НачатьЗагрузкуВыгрузки(ПредопределенноеЗначение("Справочник.ПравилаСравненияДанных.ПустаяСсылка"));
КонецПроцедуры

&НаКлиенте
Процедура ВыбратьПравилоСравненияДанных(Команда)
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ПослеВыбораПравилаСравненияДанных", ЭтотОбъект);
	ОткрытьФорму("Справочник.ПравилаСравненияДанных.ФормаВыбора",,,,,, ОповещениеОЗавершении);
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПослеВыбораПравилаСравненияДанных(ПравилоСравнения, ДополнительныеПараметры = Неопределено) Экспорт
	Если Не ЗначениеЗаполнено(ПравилоСравнения) Тогда
		Возврат;
	КонецЕсли;
	
	НачатьЗагрузкуВыгрузки(ПравилоСравнения);
КонецПроцедуры

#Область ДлительнаяОперацияЗагрузки

&НаКлиенте
Процедура НачатьЗагрузкуВыгрузки(ПравилоСравненияДанных)
	ДлительнаяОперация = НачатьЗагрузкуВыгрузкиНаСервере(АдресВыгрузки, ПравилоСравненияДанных, УникальныйИдентификатор);
	
	ПараметрыОжидания = ДлительныеОперацииКлиент.ПараметрыОжидания(ЭтотОбъект);
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ПослеЗагрузкиВыгрузки", ЭтотОбъект);
	ДлительныеОперацииКлиент.ОжидатьЗавершение(ДлительнаяОперация, ОповещениеОЗавершении, ПараметрыОжидания);
КонецПроцедуры

&НаСервереБезКонтекста
Функция НачатьЗагрузкуВыгрузкиНаСервере(Знач АдресВыгрузки, Знач ПравилоСравненияДанных, Знач ИдентификаторФормы)
	ВыгрузкаСтрокой = ПолучитьСтрокуИзДвоичныхДанных(ПолучитьИзВременногоХранилища(АдресВыгрузки));
	
	ПараметрыВыполнения = ДлительныеОперации.ПараметрыВыполненияФункции(ИдентификаторФормы);
	Возврат ДлительныеОперации.ВыполнитьФункцию(ПараметрыВыполнения, "Обработки.ЗагрузкаВыгрузокДанных.ЗагрузитьВыгрузкуДанных", ВыгрузкаСтрокой, ПравилоСравненияДанных);
КонецФункции

&НаКлиенте
Процедура ПослеЗагрузкиВыгрузки(РезультатОперации, ДополнительныеПараметры = Неопределено) Экспорт
	Если РезультатОперации = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	Если РезультатОперации.Статус = "Ошибка" Тогда
		СтандартныеПодсистемыКлиент.ВывестиИнформациюОбОшибке(РезультатОперации.ИнформацияОбОшибке);
		Возврат;
	КонецЕсли;
	
	РезультатЗагрузки = ПолучитьИзВременногоХранилища(РезультатОперации.АдресРезультата);
	
	Если РезультатЗагрузки.ТребуетсяВыборПравилаСравнения Тогда
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаВыборПравилаСравнения;
	Иначе
		СравнениеДанныхСсылка = РезультатЗагрузки.СравнениеДанныхСсылка;
		Элементы.Страницы.ТекущаяСтраница = Элементы.СтраницаРезультатЗагрузки;
	КонецЕсли;
КонецПроцедуры

#КонецОбласти

#КонецОбласти
