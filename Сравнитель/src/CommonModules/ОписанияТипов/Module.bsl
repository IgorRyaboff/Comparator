
#Область ПрограммныйИнтерфейс
// Код процедур и функций
#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс
// Код процедур и функций
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция Ссылка(Описание) Экспорт
	ОписанияТиповКлиентСервер.ОчиститьНеиспользуемыеПоля(Описание);
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ОписанияТипов.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ОписанияТипов КАК ОписанияТипов
		|ГДЕ
		|	ОписанияТипов.БазовыйТип = &БазовыйТип
		|	И ОписанияТипов.ДлинаЧисла = &ДлинаЧисла
		|	И ОписанияТипов.ТочностьЧисла = &ТочностьЧисла
		|	И ОписанияТипов.НеотрицательноеЧисло = &НеотрицательноеЧисло
		|	И ОписанияТипов.ДатаСВременем = &ДатаСВременем
		|	И ОписанияТипов.ДлинаСтроки = &ДлинаСтроки
		|	И ОписанияТипов.СтрокаНеограниченнойДлины = &СтрокаНеограниченнойДлины";
	
	Запрос.УстановитьПараметр("БазовыйТип", Описание.БазовыйТип);
	Запрос.УстановитьПараметр("ДатаСВременем", Описание.ДатаСВременем);
	Запрос.УстановитьПараметр("ДлинаСтроки", Описание.ДлинаСтроки);
	Запрос.УстановитьПараметр("ДлинаЧисла", Описание.ДлинаЧисла);
	Запрос.УстановитьПараметр("НеотрицательноеЧисло", Описание.НеотрицательноеЧисло);
	Запрос.УстановитьПараметр("СтрокаНеограниченнойДлины", Описание.СтрокаНеограниченнойДлины);
	Запрос.УстановитьПараметр("ТочностьЧисла", Описание.ТочностьЧисла);
	
	РезультатЗапроса = Запрос.Выполнить();
	
	Выборка = РезультатЗапроса.Выбрать();
	
	Если Выборка.Следующий() Тогда
		Возврат Выборка.Ссылка;
	Иначе
		Элемент = Справочники.ОписанияТипов.СоздатьЭлемент();
		ЗаполнитьЗначенияСвойств(Элемент, Описание, "БазовыйТип
		                                            |, ДлинаЧисла, ТочностьЧисла, НеотрицательноеЧисло
													|, ДлинаСтроки, СтрокаНеограниченнойДлины
													|, ДатаСВременем");
		
		Элемент.Записать();
		Возврат Элемент.Ссылка;
	КонецЕсли;
КонецФункции

Функция ОписаниеПоСсылке(Ссылка) Экспорт
	ЗначенияРеквизитов = ОбщегоНазначения.ЗначенияРеквизитовОбъекта(Ссылка, "БазовыйТип
	    |, ДлинаЧисла, ТочностьЧисла, НеотрицательноеЧисло
	    |, ДлинаСтроки, СтрокаНеограниченнойДлины
	    |, ДатаСВременем");
	
	Описание = ОписанияТиповКлиентСервер.НовоеОписаниеТипа();
	ЗаполнитьЗначенияСвойств(Описание, ЗначенияРеквизитов);
	Возврат Описание;
КонецФункции

#КонецОбласти
