
#Если Сервер Или ТолстыйКлиентОбычноеПриложение Или ВнешнееСоединение Тогда

#Область ОписаниеПеременных

#КонецОбласти

#Область ПрограммныйИнтерфейс

Процедура ЗаполнитьНовыйОбъект(ОбъектИлиДанныеФормы) Экспорт
	ОбъектИлиДанныеФормы.СпособСоединения = Перечисления.СпособыСоединенияТаблицДанных.Левое;
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытий
// Код процедур и функций
#КонецОбласти

#Область СлужебныйПрограммныйИнтерфейс
// Код процедур и функций
#КонецОбласти

#Область СлужебныеПроцедурыИФункции

Функция ВыгрузитьВОбъектXDTO(Ссылка) Экспорт
	ТаблицаXDTO = ОбщегоНазначенияСравнитель.СоздатьОбъектXDTO(1, "ТаблицаДанных");
	
	Запрос = Новый Запрос;
	Запрос.Текст = 
		"ВЫБРАТЬ
		|	ТаблицыДанных.Имя КАК Имя,
		|	ТаблицыДанных.Наименование КАК Синоним,
		|	ТаблицыДанных.ПриСравненииСтрок КАК ПриСравненииСтрок,
		|	ТаблицыДанных.СпособСоединения КАК СпособСоединения
		|ИЗ
		|	Справочник.ТаблицыДанных КАК ТаблицыДанных
		|ГДЕ
		|	ТаблицыДанных.Ссылка = &Ссылка
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ТаблицыДанныхПоля.Имя КАК Имя,
		|	ТаблицыДанныхПоля.Синоним КАК Синоним,
		|	ТаблицыДанныхПоля.ВидПоля КАК ВидПоля,
		|	ТаблицыДанныхПоля.ТипИсходногоЗначения КАК ТипИсходногоЗначения,
		|	ТаблицыДанныхПоля.ТипЗначенияСравнения КАК ТипЗначенияСравнения,
		|	ТаблицыДанныхПоля.Обязательное КАК Обязательное,
		|	ТаблицыДанныхПоля.ПриПолученииЗначенияСравнения КАК ПриПолученииЗначенияСравнения,
		|	ТаблицыДанныхПоля.ПриПолученииПредставления КАК ПриПолученииПредставления,
		|	ТаблицыДанныхПоля.ПриСравненииЗначений КАК ПриСравненииЗначений
		|ИЗ
		|	Справочник.ТаблицыДанных.Поля КАК ТаблицыДанныхПоля
		|ГДЕ
		|	ТаблицыДанныхПоля.Ссылка = &Ссылка
		|
		|УПОРЯДОЧИТЬ ПО
		|	ТаблицыДанныхПоля.НомерСтроки
		|;
		|
		|////////////////////////////////////////////////////////////////////////////////
		|ВЫБРАТЬ
		|	ПравилаВыгрузкиТаблиц.База КАК База,
		|	ПравилаВыгрузкиТаблиц.ПрименяетсяДляОбеихБаз КАК ПрименяетсяДляОбеихБаз,
		|	ПравилаВыгрузкиТаблиц.Ссылка КАК Ссылка
		|ИЗ
		|	Справочник.ПравилаВыгрузкиТаблиц КАК ПравилаВыгрузкиТаблиц
		|ГДЕ
		|	ПравилаВыгрузкиТаблиц.Владелец = &Ссылка
		|	И ПравилаВыгрузкиТаблиц.Используется";
	
	Запрос.УстановитьПараметр("Ссылка", Ссылка);
	
	РезультатЗапроса = Запрос.ВыполнитьПакет();
	
	ВыборкаШапка = РезультатЗапроса[0].Выбрать();
	ВыборкаПоля = РезультатЗапроса[1].Выбрать();
	ВыборкаПравилаВыгрузки = РезультатЗапроса[2].Выбрать();
	
	Пока ВыборкаШапка.Следующий() Цикл
		ЗаполнитьЗначенияСвойств(ТаблицаXDTO, ВыборкаШапка, "Имя, Синоним, ПриСравненииСтрок");
		ТаблицаXDTO.СпособСоединения = ОбщегоНазначения.ИмяЗначенияПеречисления(ВыборкаШапка.СпособСоединения);
	КонецЦикла;
	
	Пока ВыборкаПоля.Следующий() Цикл
		ПолеXDTO = ОбщегоНазначенияСравнитель.СоздатьОбъектXDTO(1, "ТаблицаДанных.Поле");
		ЗаполнитьЗначенияСвойств(ПолеXDTO, ВыборкаПоля, "Имя, Синоним, Обязательное
		    |, ПриПолученииЗначенияСравнения, ПриПолученииПредставления, ПриСравненииЗначений");
		
		ПолеXDTO.ВидПоля = ОбщегоНазначения.ИмяЗначенияПеречисления(ВыборкаПоля.ВидПоля);
		ПолеXDTO.ТипИсходногоЗначения = Справочники.ОписанияТипов.ВыгрузитьВОбъектXDTO(ВыборкаПоля.ТипИсходногоЗначения);
		ПолеXDTO.ТипЗначенияСравнения = Справочники.ОписанияТипов.ВыгрузитьВОбъектXDTO(ВыборкаПоля.ТипЗначенияСравнения);
		ТаблицаXDTO.Поля.Добавить(ПолеXDTO);
	КонецЦикла;
	
	Пока ВыборкаПравилаВыгрузки.Следующий() Цикл
		ПравилоXDTO = Справочники.ПравилаВыгрузкиТаблиц.ВыгрузитьВОбъектXDTO(ВыборкаПравилаВыгрузки.Ссылка);
		
		Если ВыборкаПравилаВыгрузки.ПрименяетсяДляОбеихБаз Тогда
			ТаблицаXDTO.ПравилоВыгрузки1 = ПравилоXDTO;
			
			ПравилоXDTO = Справочники.ПравилаВыгрузкиТаблиц.ВыгрузитьВОбъектXDTO(ВыборкаПравилаВыгрузки.Ссылка);
			ТаблицаXDTO.ПравилоВыгрузки2 = ПравилоXDTO;
		ИначеЕсли ВыборкаПравилаВыгрузки.База = Перечисления.НомераБаз.Первая Тогда
			ТаблицаXDTO.ПравилоВыгрузки1 = ПравилоXDTO;
		Иначе
			ТаблицаXDTO.ПравилоВыгрузки2 = ПравилоXDTO;
		КонецЕсли;
	КонецЦикла;
	
	ТаблицаXDTO.Проверить();
	Возврат ТаблицаXDTO;
КонецФункции

#КонецОбласти

#Область Инициализация

#КонецОбласти

#КонецЕсли
