
#Область ОписаниеПеременных

#КонецОбласти

#Область ОбработчикиСобытийФормы

&НаСервере
Процедура ПриСозданииНаСервере(Отказ, СтандартнаяОбработка)
	Если ЗначениеЗаполнено(Параметры.ТекущиеЗначения) Тогда
		ЗаполнитьЗначенияСвойств(ЭтотОбъект, Параметры.ТекущиеЗначения, ИменаПередаваемыхРеквизитовТЧ());
	Иначе
		Строка10 = ОписанияТиповКлиентСервер.НовоеОписаниеТипа();
		Строка10.БазовыйТип = Перечисления.БазовыеТипыОписанийТипов.Строка;
		Строка10.ДлинаСтроки = 10;
		
		ТипЗначенияСравнения = ОписанияТипов.Ссылка(Строка10);
		ТипИсходногоЗначения = ОписанияТипов.Ссылка(Строка10);
	КонецЕсли;
КонецПроцедуры

&НаКлиенте
Процедура ПередЗакрытием(Отказ, ЗавершениеРаботы, ТекстПредупреждения, СтандартнаяОбработка)
	ОповещениеОЗакрытии = Новый ОписаниеОповещения("ПередЗакрытиемПослеВопроса", ЭтотОбъект);
	ОбщегоНазначенияКлиент.ПоказатьПодтверждениеЗакрытияФормы(ОповещениеОЗакрытии, Отказ, ЗавершениеРаботы
		, ТекстПредупреждения, "ЗакрытьФормуБезПодтверждения");
	//
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиСобытийЭлементовШапкиФормы

&НаКлиенте
Процедура ТипИсходногоЗначенияНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	НачатьВыборОписанияТипа("ТипИсходногоЗначения");
КонецПроцедуры

&НаКлиенте
Процедура ТипЗначенияСравненияНажатие(Элемент, СтандартнаяОбработка)
	СтандартнаяОбработка = Ложь;
	НачатьВыборОписанияТипа("ТипЗначенияСравнения");
КонецПроцедуры

#КонецОбласти

#Область ОбработчикиКомандФормы

&НаКлиенте
Процедура Записать(Команда)
	ЗакрытьСРезультатом();
КонецПроцедуры

#КонецОбласти

#Область СлужебныеПроцедурыИФункции

&НаКлиенте
Процедура ПередЗакрытиемПослеВопроса(Результат = Неопределено, ДополнительныеПараметры = Неопределено) Экспорт
	ЗакрытьСРезультатом();
КонецПроцедуры

&НаКлиенте
Процедура ЗакрытьСРезультатом()
	Если Не ПроверитьЗаполнение() Тогда
		Возврат;
	КонецЕсли;
	
	ПараметрЗакрытия = Новый Структура(ИменаПередаваемыхРеквизитовТЧ());
	ЗаполнитьЗначенияСвойств(ПараметрЗакрытия, ЭтотОбъект);
	
	Модифицированность = Ложь;
	Закрыть(ПараметрЗакрытия);
КонецПроцедуры

&НаКлиентеНаСервереБезКонтекста
Функция ИменаПередаваемыхРеквизитовТЧ()
	Возврат "НомерСтроки, Имя, Синоним, ТипИсходногоЗначения, ТипЗначенияСравнения, ВидПоля
	        |, Обязательное, ПриПолученииЗначенияСравнения, ПриПолученииПредставления, ПриСравненииЗначений";
КонецФункции

&НаКлиенте
Процедура НачатьВыборОписанияТипа(ИмяРеквизита)
	ОповещениеОЗавершении = Новый ОписаниеОповещения("ПослеВыбораОписанияТипа", ЭтотОбъект, ИмяРеквизита);
	ОписанияТиповКлиент.НачатьВыбор(ЭтотОбъект[ИмяРеквизита], ОповещениеОЗавершении);
КонецПроцедуры

&НаКлиенте
Процедура ПослеВыбораОписанияТипа(НоваяСсылка, ИмяРеквизита) Экспорт
	Если НоваяСсылка = Неопределено Тогда
		Возврат;
	КонецЕсли;
	
	ЭтотОбъект[ИмяРеквизита] = НоваяСсылка;
	Модифицированность = Истина;
КонецПроцедуры

#КонецОбласти
